//------------Sonic CD Flip Door Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//-----Adapted to RSDKv4 by Luciano (Xeeynamo) Ciccariello----//

//-------Aliases-------//
#alias 48: TYPE_FLIPDOOR


event ObjectMain
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		if Object.PropertyValue==0
			BoxCollisionTest(C_PLATFORM, object.entityPos, -24,-4,24,32, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			BoxCollisionTest(C_TOUCH, object.entityPos, -24,-8,24,8, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			if CheckResult==1
				if object[currentPlayer].YPos>Object.YPos
					if Object.Value0==0
						//PlayStageSfx(4,0) // TODO
					end if
					Object.Value0=10
				end if
			end if
		else
			if object[currentPlayer].XPos<Object.XPos
				BoxCollisionTest(C_TOUCH, object.entityPos, -16,-24,0,24, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
				if CheckResult==1
					if Object.Value0==0
						//PlayStageSfx(4,0) // TODO
					end if
					Object.Value0=10
				end if
			else
				if Object.Value0==0
					BoxCollisionTest(C_BOX, object.entityPos, -24,-24,4,24, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
				end if
			end if
		end if
	next
end event


event ObjectDraw
	if Object.PropertyValue==0
		if Object.Value0>0
			DrawSprite(1)
			Object.Value0--
		else
			DrawSprite(0)
		end if
	else
		if Object.Value0>0
			DrawSprite(3)
			Object.Value0--
		else
			DrawSprite(2)
		end if
	end if
end event


event ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")
	SpriteFrame(-24,-4,48,8,117,34)
	SpriteFrame(-24,-44,8,48,167,43)
	SpriteFrame(-4,-24,8,48,176,43)
	SpriteFrame(-4,-24,48,8,117,34)
	ArrayPos0=32
	while ArrayPos0<1056
		if Object[ArrayPos0].Type==TypeName[FlipDoor]
			Object[ArrayPos0].DrawOrder=5
		end if
		ArrayPos0++
	loop
end event

event RSDK
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
